// precedence and associativity rules

 Name         Operators     Associates
------------------------------------
 Equality     == !=         Left
 Comparison   > >= < <=     Left
 Term         - +           Left
 Factor       / *           Left
 Unary        ! - +         Right


// precedence increases downwards
// each rule matches expressions at its precedence level or higher

program       -> declaration* EOF ;

declaration   -> varDecl
              |  statement ;

varDecl       -> "let" IDENTIFIER ( "=" expression )? ";" ;

statement     -> exprStmt
              |  ifStmt
              |  printStmt
              |  whileStmt
              |  block ;

exprStmt      -> expression ";" ;

ifStmt        -> "if" "(" expression ")" statement
               ( "else" statement )? ;

printStmt     -> "print" expression ";" ;

whileStmt     -> "while" "(" expression ")" statement ;

block         -> "{" declaration* "}" ;

expression    -> assignment ;

assignment    -> IDENTIFIER "=" assignment
              |  logic_or ;

logic_or      -> logic_and ( "or" logic_and )* ;

logic_and     -> equality ( "and" equality )* ;

equality      -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison    -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term          -> factor ( ( "-" | "+" ) factor )* ;

factor        -> unary ( ( "/" | "*" ) unary )* ;

unary         -> ( "+" | "-" | "!" ) unary
              |  primary ;

primary       -> "true" | "false" | "void"
              |  NUMBER | STRING
              |  "(" expression ")"
              |  IDENTIFIER ;
