expression  -> literal
            |  unary
            |  binary
            |  grouping ;

literal     -> NUMBER | STRING | "true" | "false" | "void" ;

grouping    -> "(" expression ")" ;

unary       -> ( "+" | "-" | "!" ) expression ;

binary      -> expression operator expression ;

operator    -> "==" | "!=" | "<" | "<=" | ">" | ">="
            |  "+"  | "-"  | "*" | "/" ;


// precedence and associativity rules

 Name         Operators     Associates
------------------------------------
 Equality     == !=         Left
 Comparison   > >= < <=     Left
 Term         - +           Left
 Factor       / *           Left
 Unary        ! - +         Right


// precedence increases downwards
// each rule matches expressions at its precedence level or higher

program       -> declaration* EOF ;

declaration   -> varDecl
              |  statement ;

varDecl       -> "let" IDENTIFIER ( "=" expression )? ";" ;

statement     -> exprStmt
              |  printStmt ;

exprStmt      -> expression ";" ;

printStmt     -> "print" expression ";" ;

expression    -> assignment ;

assignment    -> IDENTIFIER "=" assignment
              |  equality ;

equality      -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison    -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term          -> factor ( ( "-" | "+" ) factor )* ;

factor        -> unary ( ( "/" | "*" ) unary )* ;

unary         -> ( "+" | "-" | "!" ) unary
              |  primary ;

primary       -> "true" | "false" | "void"
              |  NUMBER | STRING
              |  "(" expression ")"
              |  IDENTIFIER ;
